---
layout: blogpost
draft: true
title: "D for the Win"
description: "The language I've been always looking for"
imageurl: http://tomerfiliba.com/static/res/2014-08-20-darkside.png
imagelink: http://frenchtouch29.deviantart.com/art/JOIN-THE-DARK-SIDE-212700850
imagetitle: "It's time to switch sides"
---

I'm a convert! I've seen the light!

You see, Python is nice and all, and it excels in so many domains,
but when you take it out of the lab and into the real world, you get the feeling the price you pay
is just too high. Literally. You burn too much memory and CPU cycles doing very little work...
and there are times you just can't afford it.

The [C10M problem](http://c10m.robertgraham.com/p/manifesto.html) is a reiteration of the
[C10K problem](http://www.kegel.com/c10k.html). In short, today's hardware can handle millions
of packets per second, but in reality you hardly ever reach these numbers. For instance,
I worked a short while at a company that used AWS and had lots Python servers accepting requests
and logging them, not doing any actual work. They used `twisted` and managed to squeeze ~500 requests/sec.
Moving to PyPy (not without discomfort) tripled the numbers or so, but still, the cost simply didn't scale.

Python, I love you, but you help instill [Gate's law](http://en.wikipedia.org/wiki/Wirth's_law) --
"The speed of software halves every 18 months". In the end, we pay for our CPU cycles and we want to
maximize our profit. It's not you, Guido, it's me. I've moved on to the C10M world, and for that
I'd need a programming language that's designed for **system programming** with a strong and modern
**type system** (after all, I love duck typing). I need to interface with external systems,
so a C ABI is desirable (no foreign function interface), and meta-programming is a huge plus (so I
won't need to include code-generation in my build system).

I've looked at [rust](http://www.rust-lang.org/) (nice, but will require a couple of years to mature enough
for a large-scale project), [go](http://golang.org/) (they must be joking if they actually consider it
for system programming), and at last, I've finally found peace with [D](http://dlang.org/).

## Dlang Dlang Ãœber Alles ##

<img src="http://tomerfiliba.com/static/res/2014-03-27-dlogo.png" title="Dlang" class="blog-post-image">

System programming is an endless topic with all sorts of constraints imposed by your specific environment,
which brings up all sorts o weird issues and problems to tackle. In other words, boring.
Instead of writing a post about how D serves me in that, I'd rather show how **D speaks fluent Python**.

But first things first. In (the probable) case you don't know much D -- imagine it's what C++ would have
wished to be. It offers better syntax, much faster compilation time, optional garbage collection, highly
expressive templates and type inference, Pythonic operator overloading (by *rewriting* the expression),
object-oriented and functional (multi-paradigm like Python), intermingles high-level constructs with
low-level ones (naked function in inline assembly) to produces efficient code, has compile-time
introspection capabilities and some extra cools features in the domain of code generation:
`mixin` -- which evaluates an arbitrary string of D code at compile time, and `CTFE` -- compile-time
function execution.

In general, D follows Python's duck-typed (or protocol-oriented) spirit. If a type provides the
necessary interface ("protocol") for an operation, it will *just work*, but you can also test for
compliance at compile time. For example, *ranges* are a generalization of generators in Python.
All you need to do in order to be an `InputRange` is implement `bool empty()`, `void popFront()`
and `auto front()`, and you can use `isInputRange!T` to test whether `T` adheres the protocol.
By the way, the exclamation point (`!`) marks compile-time arguments -- there's a clear separation
in D of what's compile-time and what's runtime.

For brevity's sake, I'm not going to demonstrate all the properties I listed up there. Instead, I'll
show why Python programmers ought to love D.

## Case Study #1: Generating HTML ##

In an [old blog post](http://tomerfiliba.com/blog/Hypertext/#the-code) I outlined my vision of
HTML templating languages: there should be none. I argued they are all but crippled-down forms of
Python with an ugly syntax, so just give me Python and an easy way to programmatically manipulate the DOM.

I've later extended the concept into a library in its own right, called `srcgen`. You can use it to generate
[HTML](https://github.com/tomerfiliba/srcgen/blob/master/tests/test_html.py),
[C-like](https://github.com/tomerfiliba/srcgen/blob/master/tests/test_c.py) languages and Python/Cython code.
That's what I use when I need to generate code.

So here's an excerpt of how's it done in `srcgen`:

{% highlight python %}
def buildPage():
    doc = HtmlDocument()
    with doc.head():
        doc.title("das title")
        doc.link(rel = "foobar", type="text/css")

    with doc.body():
        with doc.div(class_="mainDiv"):
            with doc.ul():
                for i in range(5):
                    with doc.li(id = str(i), class_="listItem"):
                        doc.text("I am bulletpoint #", i)

    return doc.render()
{% endhighlight %}

And here's how it's done in D:

{% highlight d %}
auto buildPage() {
    auto doc = new Html();

    with (doc) {
        with (head) {
            title("das title");
            link[$.rel = "foobar", $.type = "text/css"];
        }
        with (body_) {
            with(div[$.class_ = "mainDiv"]) {
                with (ul) {
                    foreach(i; 0 .. 5) {
                        with (li[$.id = i, $.class_ = "listItem"]) {
                            text("I am bulletpoint #");
                            text(i);
                        }
                    }
                }
            }
        }
    }

    return doc.render();
}
{% endhighlight %}

You can find the [source code](https://github.com/tomerfiliba/dlang/blob/9bb0902809570742c070723a0a7e684350c352e1/source/html.d#L150)
on github, just keep in mind it's a sketch I wrote for this blog post, not a feature-complete library.

The funny thing is, Python's `with` and D's `with` are not even related! The Python implementation builds
a stack of *context managers*, while `with` in D merely alters symbol lookup. But lo and behold!
The two versions are practically the same code (modulo curly braces). You get the same expressive power in both.

## Case Study #2: Construct ##

But the pinnacle is clearly my D version of [Construct](http://construct.readthedocs.org/en/latest/index.html).
You see, I've been [straggling for many years](https://code.google.com/p/sebulbasvn/source/browse/#svn%2Ftrunk%2Fccon)
to create a compiled version of Construct. Generating efficient, static code from declarative constructs
would make Construct capable of handling real-world data, like packet sniffing or processing of large files.

The issues with the C version of Construct were numerous, but they basically boiled down to the fact I needed
a stronger object model to represent strings, dynamic arrays, etc., and [adapters](http://construct.readthedocs.org/en/latest/adapters.html).
The real power of Construct came from adapters, which let you operate at the representational ("DOM") level
of the data, rather on its binary form. That required lambdas, closures and other higher-level concepts that C lacks.
I even tried writing a Haskell version, given that Haskell is so high-level and functional, but my colleague
and I had given hope after a while.

Last week, it struck me that D could be the perfect candidate: it has all the necessary high-level concepts while
being able to generate efficient code with meta-programming. I toyed around with a D version, which proved
to be extremely promising. So without further ado, I present [dconstruct](https://github.com/tomerfiliba/dlang/blob/9bb0902809570742c070723a0a7e684350c352e1/source/dconstruct.d#L260) --
an initial sketch of the library.

This is the canonical `PascalString` declaration in Python:

{% highlight pycon %}
>>> pascal_string = Struct("pascal_string",
...     UBInt8("length"),
...     Array(lambda ctx: ctx.length, Field("data", 1),),
... )
>>>
>>> pascal_string.parse("\x05helloXXX")
Container({'length': 5, 'data': ['h', 'e', 'l', 'l', 'o']})
>>>
>>> pascal_string.build(Container(length=5, data="hello"))
'\x05hello'
{% endhighlight %}

And here's how it's done in D:

{% highlight d %}
struct PascalString {
    Field!ubyte length;
    Array!(Field!ubyte, "length") data;

    // the equivalent of 'Struct' in Python,
    // to avoid confusion of keyword 'struct' and 'Struct'
    mixin Record;
}

PascalString ps;
auto stream = cast(ubyte[])"\x05helloXXXX".dup;
ps.unpack(stream);
writeln(ps);
// {length: 5, data: [104, 101, 108, 108, 111]}
{% endhighlight %}

Though the use of meta-programming (and some help from the Dark Side of the Force), that code snippet there
actually boils down (after inlining and optimizations) to something like

{% highlight d %}
struct PascalString {
    ubyte length;
    ubyte[] data;

    void unpack(ref ubyte[] stream) {
        length = stream[0];
        stream = stream[1 .. $]; // advance stream
        data = stream[0 .. length];
        stream = stream[length .. $];  // advance stream
    }
}
{% endhighlight %}

<img src="http://tomerfiliba.com/static/res/2014-08-20-theresmore.jpg" title="There's more" class="blog-post-image">

But wait, there's more! The real beauty here is how we handle the
[context](http://construct.readthedocs.org/en/latest/meta.html). In Python, Construct builds a dictionary
that travels around the with process, allowing constructs to refer to previously seen objects. This is possible
in D too, of course, but it's highly inefficient (and not type safe). Instead, `dconstruct` uses a trick that's
commonly found in templated languages -- creating types on demand:

{% highlight d %}
struct Context(T, U) {
    T* _curr;
    U* _;
    alias _curr this;   // see blow
}

auto linkContext(T, U)(ref T curr, ref U parent) {
    return Context!(T, U)(&curr, &parent);
}
{% endhighlight %}

The strange `alias _curr this` is a lovely feature of D known as `subtyping`. It basically means that
any property that doesn't exist at the current scope will we forwarded to `_curr`, e.g.,
if I type `myCtx.foo` and `myCtx` doesn't define `foo`, the call will be rewritten as `myCtx._curr.foo`.

As we travel along constructs, we link the existing context with its ancestor (`_). This means that for
each "level of nesting" of constructs, we get a uniquely-typed context. At runtime, this context is nothing
more than a pair of pointers, but at compile time it keeps you type-safe. In other words, you can't misspell
a field's name and expect the program to compile.

A more interesting example would thus be

{% highlight d %}
struct MyStruct {
    Field!ubyte length;
    YourStruct child;

    mixin Record!();
}

struct YourStruct {
    Field!ubyte whatever;
    Array!(Field!ubyte, "_.length") data;      // go one level up, then look at 'length'

    mixin Record;
}

MyStruct ms;
ms.unpack(stream);
{% endhighlight %}

When we unpack `MyStruct` (which recursively unpacks `YourStruct`), a new context `ctx` will be created
with `ctx._curr = &ms.child` and `ctx._ = &ms`. When `YourStruct` refers to `"_.length"`, the string
is implanted into `ctx`, yielding `ctx._.length`. If we refered to the wrong path or misspelled anything,
it would simply not compile. That, and you don't need dictionary lookups at runtime --
it's all resolved in compile time.

So again, this is a very preliminary version of Construct, miles away from production grade,
but you can already see where it's going.

By the way, you can try out D online at [dpaste](http://dpaste.dzfl.pl) and even play around with my
demo version of dconstruct [over there](http://dpaste.dzfl.pl/f0fccc09707e).



